plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

allprojects {
    apply plugin: 'idea'
    apply plugin: 'java-library'

    group = 'fr.hyriode'

    java {
        withSourcesJar()
        withJavadocJar()

        toolchain {
            languageVersion = JavaLanguageVersion.of(18)
        }
    }

    compileJava {
        sourceCompatibility = JavaVersion.VERSION_18
        options.encoding = 'UTF-8'
    }

    tasks.withType(Javadoc) {
        options.encoding = 'UTF-8'
    }
}

version = '1.0.0'
archivesBaseName = 'Hylios'

configurations.all {
    // Check for updates every build
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

repositories {
    mavenCentral()

    maven { url 'https://repo1.maven.org/maven2' }

    maven {
        name = 'HyriAPI_maven'
        url = 'https://maven.pkg.github.com/Hyriode/HyriAPI/'

        credentials {
            username System.getenv("GITHUB_USERNAME")
            password System.getenv("GITHUB_TOKEN")
        }
    }
}

shadowJar {
    configurations = [project.configurations.runtimeClasspath]
    archiveVersion.set('')
    manifest {
        attributes 'Main-Class': 'fr.hyriode.hylios.HyliosBootstrap'
    }
}

dependencies {
    implementation "com.influxdb:influxdb-client-java:6.6.0"
    implementation 'fr.hyriode:hyriapi-app:1.5.2-SNAPSHOT'
    implementation 'jline:jline:2.14.6'
}